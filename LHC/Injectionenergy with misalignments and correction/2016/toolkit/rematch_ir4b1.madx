
scale = 23348.89927;
scmin := 0.03*7000./nrj;
qtlimit2   := 1.0*160.0/scale;
qtlimit3   := 1.0*200.0/scale;
qtlimit4   := 1.0*125.0/scale;
qtlimit5   := 1.0*120.0/scale;
qtlimit6   := 1.0*90.0/scale;

if(on_holdselect==0){ exec,select(4,34,45,b1); };

if(on_fixedip==1){
muxip4b1=2.1600 ;betxip4b1 = 172.807960 ;alfxip4b1 =  0.580679 ;
muyip4b1=1.7200 ;betyip4b1 = 230.297901 ;alfyip4b1 = -0.325498 ;
dxip4b1 = 0.0000 ;dpxip4b1 = 0.0000 ;
};
value,muxip4b1,betxip4b1,alfxip4b1,muyip4b1,betyip4b1,alfyip4b1,dxip4b1,dpxip4b1;

use,period=lhcb1,range=s.ds.l4.b1/e.ds.r4.b1;
match, sequence=lhcb1, beta0=bir4b1;
constraint,sequence=lhcb1,range=ip4,dpx =dpxip4b1, dx=dxip4b1;
constraint,sequence=lhcb1,range=ip4,betx=betxip4b1;
constraint,sequence=lhcb1,range=ip4,bety=betyip4b1;
constraint,sequence=lhcb1,range=ip4,alfx=alfxip4b1,alfy=alfyip4b1;
constraint,sequence=lhcb1,range=e.ds.r4.b1,alfx=eir4b1->alfx,alfy=eir4b1->alfy;
constraint,sequence=lhcb1,range=e.ds.r4.b1,betx=eir4b1->betx,bety=eir4b1->bety;
constraint,sequence=lhcb1,range=e.ds.r4.b1,dx=eir4b1->dx,dpx=eir4b1->dpx;
constraint,sequence=lhcb1,range=e.ds.r4.b1,muy=muyip4b1+eir4b1->muy;
constraint,sequence=lhcb1,range=e.ds.r4.b1,mux=muxip4b1+eir4b1->mux;
if (is_thin==1){
constraint,sequence=lhcb1,range=IP4,mux=muxIP4b1_l,muy=muyIP4b1_l;
};

if(match_on_aperture==1){
constraint,sequence=lhcb1, range=mq.12l4.b1,bety<182.5-2.0+2.0;
constraint,sequence=lhcb1, range=mqml.10l4.b1,bety<178.5;
constraint,sequence=lhcb1, range=mqml.8l4.b1,bety<178.5-0.5;
constraint,sequence=lhcb1, range=mqy.6l4.b1,bety<600;
constraint,sequence=lhcb1, range=mqy.5l4.b1,betx<460-2;
constraint,sequence=lhcb1, range=mqy.5r4.b1,bety<490;
constraint,sequence=lhcb1, range=mqy.6r4.b1,betx<600.;
constraint,sequence=lhcb1, range=mqm.7r4.b1,bety<178.5;
constraint,sequence=lhcb1, range=mqm.9r4.b1,bety<171.5;
constraint,sequence=lhcb1, range=mq.11r4.b1,bety<181.8+0.1;
constraint,sequence=lhcb1, range=mq.12r4.b1,betx<176.5-2.0;
};
 vary,name=kqt13.l4b1,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
 vary,name=kqt12.l4b1,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
 vary,name=kqtl11.l4b1, step=1.0E-6, lower=-qtlimit4, upper= qtlimit4;
 vary,name=kq10.l4b1,   step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
 vary,name=kq9.l4b1,    step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
 vary,name=kq8.l4b1,    step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
 vary,name=kq7.l4b1,    step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
 vary,name=kq6.l4b1,    step=1.0E-6, lower=-qtlimit2, upper=-qtlimit2*scmin;
 vary,name=kq5.l4b1,    step=1.0E-6, lower= qtlimit2*scmin, upper= qtlimit2;

 vary,name=kq5.r4b1,    step=1.0E-6, lower=-qtlimit2, upper=-qtlimit2*scmin;
 vary,name=kq6.r4b1,    step=1.0E-6, lower= qtlimit2*scmin, upper= qtlimit2;
 vary,name=kq7.r4b1,    step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
 vary,name=kq8.r4b1,    step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
 vary,name=kq9.r4b1,    step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
 vary,name=kq10.r4b1,   step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
 vary,name=kqtl11.r4b1, step=1.0E-6, lower=-qtlimit4, upper= qtlimit4;
 vary,name=kqt12.r4b1,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
 vary,name=kqt13.r4b1,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
jacobian,calls=jac_calls, tolerance=jac_tol, bisec=jac_bisec;
!simplex,  calls=15, tolerance=jac_tol;-
!lmdif,calls=200,tolerance=1.e-21;
endmatch;

exec,mk_irtwiss(4,b1);

value,table(twiss,BPMC.9L4.B1,betx),table(twiss,BPMC.7L4.B1,betx);
value,table(twiss,BPMC.9R4.B1,bety),table(twiss,BPMCA.7R4.B1,bety);
value,table(twiss,MKQA.6L4.B1,betx),table(twiss,MKQA.6L4.B1,bety);
value,table(twiss,BQKH.B6L4.B1,betx),table(twiss,BQKV.6L4.B1,bety);
value,table(twiss,ADTKH.D5L4.B1,betx),table(twiss,ADTKH.C5L4.B1,betx),
      table(twiss,ADTKH.B5L4.B1,betx),table(twiss,ADTKH.A5L4.B1,betx);
value,table(twiss,ADTKV.A5R4.B1,bety),table(twiss,ADTKV.B5R4.B1,bety),
      table(twiss,ADTKV.C5R4.B1,bety),table(twiss,ADTKV.D5R4.B1,bety);

tarir4b1=tar;
value,kqt13.l4b1,kqt12.l4b1,kqtl11.l4b1,kq10.l4b1,kq9.l4b1,kq8.l4b1,kq7.l4b1,kq6.l4b1,kq5.l4b1,kq5.r4b1,kq6.r4b1,kq7.r4b1,kq8.r4b1,kq9.r4b1,kq10.r4b1,kqtl11.r4b1,kqt12.r4b1,kqt13.r4b1;
value,tarir4b1;

!muxip4b1=2.1600 ;betxip4b1 = 172.807960 ;alfxip4b1 =  0.580679 ;
!muyip4b1=1.7200 ;betyip4b1 = 230.297901 ;alfyip4b1 = -0.325498 ;
!dxip4b1 = 0.0000 ;dpxip4b1 = 0.0000 ;
!constraint, sequence=lhcb1, range=mqm.7r4.b1 ,bety<1265;
!constraint, sequence=lhcb1, range=mqy.6l4.b1,bety<1620;
!strategy for 11->22 :block the right and kqt12.r4b1 till 0.82/0.82, then kqt13.r4b1 till 0.5/0.5 with disp, beta and alpha as injection
!strategy for 22->28 : block the right and kqt13.r4b1  with disp, beta and alpha as injection
!strategy for 22->82 : all blocking kq8.r4b1 at some point and avoiding too small kq7.r4b1 by decreasing betxIP4 by 23 %


