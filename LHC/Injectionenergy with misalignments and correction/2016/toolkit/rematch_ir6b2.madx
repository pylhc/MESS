

scale = 23348.89927;
scmin := 0.03*7000./nrj;
qtlimitx28 := 1.0*225.0/scale;
qtlimitx15 := 1.0*205.0/scale;
qtlimit2 := 1.0*160.0/scale;
qtlimit3 := 1.0*200.0/scale;
qtlimit4 := 1.0*125.0/scale;
qtlimit5 := 1.0*120.0/scale;
qtlimit6 := 1.0*90.0/scale;


if(on_holdselect==0){ exec,select(6,56,67,b2); };

if(on_fixedip==1){
muxip6b2 = 2.0000 ;betxip6b2= 187.749224;alfxip6b2 = 0.551968;
muyip6b2 = 2.0300 ;betyip6b2= 178.368556;alfyip6b2 =-0.607183;
dxip6b2 =  0.186;dpxip6b2 = -0.000256 ;
};

value,muxip6b2,betxip6b2,alfxip6b2;
value,muyip6b2,betyip6b2,alfyip6b2;
value,dxip6b2,dpxip6b2;

use,sequence=lhcb2,range=s.ds.l6.b2/e.ds.r6.b2;
match,      sequence=lhcb2, beta0=bir6b2;
weight,mux=10,muy=10;
constraint,sequence=lhcb2,range=ip6,betx=betxip6b2;
constraint,sequence=lhcb2,range=ip6,bety=betyip6b2;
constraint,sequence=lhcb2,range=ip6,dx=dxip6b2;
constraint,sequence=lhcb2,range=ip6,dpx=dpxip6b2;
constraint,sequence=lhcb2,range=ip6,alfx=alfxip6b2;
!if(is_thin==0){
constraint,sequence=lhcb2,range=ip6,alfy=alfyip6b2;
!};
!if(is_thin==1){
!  constraint,sequence=lhcb2,range=ip6,dx=dxip6b2,dpx =dpxip6b2;
!};
constraint,sequence=lhcb2,range=e.ds.r6.b2,alfx=eir6b2->alfx,alfy=eir6b2->alfy;
constraint,sequence=lhcb2,range=e.ds.r6.b2,betx=eir6b2->betx,bety=eir6b2->bety;
constraint,sequence=lhcb2,range=e.ds.r6.b2,dx=eir6b2->dx,dpx=eir6b2->dpx;
constraint,sequence=lhcb2,range=e.ds.r6.b2,mux=muxip6b2+eir6b2->mux;
constraint,sequence=lhcb2,range=e.ds.r6.b2,muy=muyip6b2+eir6b2->muy;
if (is_thin==1){
constraint,sequence=lhcb2,range=IP6,mux=muxIP6b2_l,muy=muyIP6b2_l;
};
if(match_on_aperture==1){
!constraint,range=mq.12l6.b2,dx< 2.17;
constraint,range=mq.11l6.b2,bety< 178.5+epsap;
constraint,range=mqml.10l6.b2,betx< 175.2+epsap;
constraint,range=mqm.9l6.b2,bety< 180+epsap;
constraint,range=MQY.5L6.B2,bety<577+epsap+23;
constraint,range=MQY.4L6.B2,betx<577+epsap+23;
constraint,range=MQY.4R6.B2,bety<588+epsap+23;
constraint,range=MQY.5R6.B2,betx<588+epsap+23; !,dx<0.215;
constraint,range=mcbv.12r6.b2,bety< 180.2+epsap;
constraint,range=mqml.10r6.b2,bety< 178.5+1+1.4;
constraint,range=mqml.10l6.b2,betx< 175.2+epsap+1;
};
vary, name=kqt13.l6b2,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
vary, name=kqt12.l6b2,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
vary, name=kqtl11.l6b2, step=1.0E-6, lower=-qtlimit4*400./550., upper=qtlimit4*400./550.;
vary, name=kq10.l6b2,   step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
vary, name=kq9.l6b2,    step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
vary, name=kq8.l6b2,    step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
vary, name=kq5.l6b2,    step=1.0E-6, lower=-qtlimit2, upper=-qtlimit2*scmin;
vary, name=kq4.l6b2,    step=1.0E-6, lower= qtlimit2*scmin, upper= qtlimit2;

if (is_thin==1){
vary,name=kq4.r6b2,    step=1.0E-6, lower=-qtlimit2, upper=-qtlimit2*scmin;
};
vary, name=kq5.r6b2,    step=1.0E-6, lower= qtlimit2*scmin, upper= qtlimit2;
vary, name=kq8.r6b2,    step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin*0.8;
vary, name=kq9.r6b2,    step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
vary, name=kq10.r6b2,   step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
vary, name=kqtl11.r6b2, step=1.0E-6, lower=-qtlimit4*300./550., upper=qtlimit4*300./550.;
!if (is_thin==0){
vary, name=kqt12.r6b2,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
!};
vary, name=kqt13.r6b2,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
!lmdif,calls=200,tolerance=1.e-21;
jacobian,calls=jac_calls, tolerance=jac_tol, bisec=jac_bisec;
!simplex,  calls=15, tolerance=jac_tol;-
endmatch;

exec,mk_irtwiss(6,b2);
value,kq4.r6b2+0.004833837730000;
dmuxkickb2=table(twiss,MKD.H5R6.B2,mux)-table(twiss,TCSG.4L6.B2,mux);
value,dmuxkickb2;
al_dump=761;
bxdumpb2=refbetxip6b2+2*al_dump*refalfxip6b2+
                        al_dump^2*(1+refalfxip6b2^2)/refbetxip6b2;
bydumpb2=refbetyip6b2+2*al_dump*refalfyip6b2+
                        al_dump^2*(1+refalfyip6b2^2)/refbetyip6b2;
value,bxdumpb2,bydumpb2;

tarir6b2=tar;
value,tarir6b2;
value,kqt13.l6b2,kqt12.l6b2,kqtl11.l6b2,kq10.l6b2,kq9.l6b2,kq8.l6b2,kq5.l6b2,kq4.l6b2,
kq4.r6b2,kq5.r6b2,kq8.r6b2,kq9.r6b2,kq10.r6b2,kqtl11.r6b2,kqt12.r6b2,kqt13.r6b2;

!muxip6b2 = 2.0000 ;betxip6b2= 1.96000000E+02 ;alfxip6b2 = 5.98285418E-01  ;
!muyip6b2 = 2.0300 ;betyip6b2= 1.79082800E+02;alfyip6b2 =-5.41937487E-01 ;
!dxip6b2 =  1.74610979E-01;dpxip6b2 = 1.56146811E-03 ;
!strategy for 11->22 : use all (kq4.r6b2 fixed),alfas,dx as injection,dpx=0,betas not constrained
!strategy for 22->28 : use all (kq4.r6b2 fixed),alfas, dx as injection,dpx=0,betas not constrained
!strategy for 22->82 : use all (kq4.r6b2 fixed),alfas, dx as injection,dpx=0,betas not constrained (around 320/180)


