
! fine tuning of coupling   (S.Fartoukh)

! knob from db5/toolkit/solenoid_comp.b2.as-built.madx
! store initial skew quad strengths
 kqs.a12b2_0 = kqs.a12b2;
 kqs.r2b2_0  = kqs.r2b2 ;
 kqs.l3b2_0  = kqs.l3b2 ;
 kqs.a34b2_0 = kqs.a34b2;
 kqs.r4b2_0  = kqs.r4b2 ;
 kqs.l5b2_0  = kqs.l5b2 ;
 kqs.a56b2_0 = kqs.a56b2;
 kqs.r6b2_0  = kqs.r6b2 ;
 kqs.l7b2_0  = kqs.l7b2 ;
 kqs.a78b2_0 = kqs.a78b2;
 kqs.r8b2_0  = kqs.r8b2 ;
 kqs.l1b2_0  = kqs.l1b2 ;

! knob definitions by S.Fartoukh's program
! V6.503.as-built with missing sector 34
 B11=0.12495491E-01;  B12=0.11669974E-01;  // 27/11/2009 11:15:44
 B21=-.28764787E-02;  B22=-.26989904E-01;
 B31=0.00000000E+00;  B32=0.00000000E+00;  // disabled
 B41=0.19451171E-01;  B42=-.74638628E-02;
 B51=-.17311062E-01;  B52=0.25969300E-01;
 B61=0.15913428E-01;  B62=0.56609656E-02;
 B71=0.19259371E-01;  B72=-.14604723E-01;
 B81=-.34710377E-02;  B82=-.26384288E-01;

! apply increments to skew quads
 kqs.a12b2 := kqs.a12b2_0 + b11*cmrskew + b12*cmiskew;
 kqs.r2b2  := kqs.r2b2_0  + b21*cmrskew + b22*cmiskew;
 kqs.l3b2  := kqs.l3b2_0  + b21*cmrskew + b22*cmiskew;
 kqs.a34b2 := kqs.a34b2_0 + b31*cmrskew + b32*cmiskew;
 kqs.r4b2  := kqs.r4b2_0  + b41*cmrskew + b42*cmiskew;
 kqs.l5b2  := kqs.l5b2_0  + b41*cmrskew + b42*cmiskew;
 kqs.a56b2 := kqs.a56b2_0 + b51*cmrskew + b52*cmiskew;
 kqs.r6b2  := kqs.r6b2_0  + b61*cmrskew + b62*cmiskew;
 kqs.l7b2  := kqs.l7b2_0  + b61*cmrskew + b62*cmiskew;
 kqs.a78b2 := kqs.a78b2_0 + b71*cmrskew + b72*cmiskew;
 kqs.r8b2  := kqs.r8b2_0  + b81*cmrskew + b82*cmiskew;
 kqs.l1b2  := kqs.l1b2_0  + b81*cmrskew + b82*cmiskew;



! saved on 13/10/08 15:49:08 by T.Risselada

! closest tune
 kqtf.b20=kqtf.b2;
 kqtd.b20=kqtd.b2;
 qmid=(qx0-qx00+qy0-qy00)*0.5;

 match;
 global, q1=qx00+qmid,q2=qy00+qmid;
 vary,   name=kqtf.b2, step=1.E-9;
 vary,   name=kqtd.b2, step=1.E-9;
 lmdif,  calls=50, tolerance=1.E-5;
 endmatch;


! Quick minimization based on linear machine
 cmrskew0 = cmrskew;
 cmiskew0 = cmiskew;
 twiss; qx=table(summ,q1); qy=table(summ,q2);

 cta0     = abs(qx-qy-tsplit);
 closest0 = cta0;
 cmrskew  = cmrskew0+cta0/2.;
 twiss; qx=table(summ,q1); qy=table(summ,q2);

 ctap     = abs(qx-qy-tsplit);
 cmrskew  = cmrskew0-cta0/2.;
 twiss; qx=table(summ,q1); qy=table(summ,q2);

 ctam     = abs(qx-qy-tsplit);
 cmrskew  = cmrskew0+(ctam^2-ctap^2)/2./cta0;
 twiss; qx=table(summ,q1); qy=table(summ,q2);

 cta0     = abs(qx-qy-tsplit);
 cmiskew  = cmiskew0+cta0/2.;
 twiss; qx=table(summ,q1); qy=table(summ,q2);

 ctap     = abs(qx-qy-tsplit);
 cmiskew  = cmiskew0-cta0/2.;
 twiss; qx=table(summ,q1); qy=table(summ,q2);

 ctam     = abs(qx-qy-tsplit);
 cmiskew  = cmiskew0+(ctam^2-ctap^2)/2./cta0;
 twiss; qx=table(summ,q1); qy=table(summ,q2);

 closest1 =abs(qx-qy-tsplit);


!Empirical minimisation
 match;
 global, q1=qx00+qmid, q2=qy00+qmid;
 vary,   name=kqtf.b2, step=1.E-9;
 vary,   name=kqtd.b2, step=1.E-9;
 lmdif,  calls=100,tolerance=1.E-6;
 endmatch;

 match;
 global, q1=qx00+qmid, q2=qy00+qmid;
 vary,   name=cmrskew, step=1.E-9;
 vary,   name=cmiskew, step=1.E-9;
 lmdif,  calls=150, tolerance=2.E-6;
 endmatch;

 match;
 global, q1=qx00+qmid, q2=qy00+qmid;
 vary,   name=kqtf.b2, step=1.E-9;
 vary,   name=kqtd.b2, step=1.E-9;
 lmdif,  calls=100, tolerance=1.E-7;
 endmatch;

 match;
 global, q1=qx00+qmid, q2=qy00+qmid;
 vary,   name=cmrskew, step=1.E-9;
 vary,   name=cmiskew, step=1.E-9;
 lmdif,  calls=150, tolerance=2.E-7;
 endmatch;

 match;
 global, q1=qx00+qmid, q2=qy00+qmid;
 vary,   name=kqtf.b2, step=1.E-9;
 vary,   name=kqtd.b2, step=1.E-9;
 lmdif,  calls=100, tolerance=1.E-7;
 endmatch;

 twiss;  qx=table(summ,q1); qy=table(summ,q2);
 closest2=abs(qx-qy-tsplit);
 value, closest0, closest1, closest2, cmrskew, cmiskew;

return;
