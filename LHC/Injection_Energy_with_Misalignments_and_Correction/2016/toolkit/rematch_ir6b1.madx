scale = 23348.89927;
scmin := 0.03*7000./nrj;
qtlimitx28 := 1.0*225.0/scale;
qtlimitx15 := 1.0*205.0/scale;
qtlimit2 := 1.0*160.0/scale;
qtlimit3 := 1.0*200.0/scale;
qtlimit4 := 1.0*125.0/scale;
qtlimit5 := 1.0*120.0/scale;
qtlimit6 := 1.0*90.0/scale;


if(on_holdselect==0){ exec,select(6,56,67,b1); };

if(on_fixedip==1){
muxip6b1 = 2.0000 ;betxip6b1= 187.297499 ;alfxip6b1 = -0.541994 ;
muyip6b1 = 2.0300 ;betyip6b1= 168.122917 ;alfyip6b1 =  0.605891 ;
dxip6b1 =  0.1400 ;dpxip6b1 =   0 ;
};
value,muxip6b1,betxip6b1,alfxip6b1;
value,muyip6b1,betyip6b1,alfyip6b1;
value,dxip6b1,dpxip6b1;

use,sequence=lhcb1,range=s.ds.l6.b1/e.ds.r6.b1;
match,      sequence=lhcb1, beta0=bir6b1;
weight,mux=10,muy=10;
constraint,sequence=lhcb1,range=ip6,bety=betyip6b1;
constraint,sequence=lhcb1,range=ip6,betx=betxip6b1;
constraint,sequence=lhcb1,range=ip6,dx =dxip6b1;
constraint,sequence=lhcb1,range=ip6,dpx=dpxip6b1;
constraint,sequence=lhcb1,range=ip6,alfx=alfxip6b1;
!if(is_thin==0){
constraint,sequence=lhcb1,range=ip6,alfy=alfyip6b1;
!};
!constraint,range=mqml.10l6.b1,bety< 700;
!constraint,range=mqm.9l6.b1,betx< 1250;
!if(is_thin==1){
!  constraint,sequence=lhcb1,range=ip6,betx=betxip6b1;
!};
constraint,sequence=lhcb1,range=e.ds.r6.b1,alfx=eir6b1->alfx,alfy=eir6b1->alfy;
constraint,sequence=lhcb1,range=e.ds.r6.b1,betx=eir6b1->betx,bety=eir6b1->bety;
constraint,sequence=lhcb1,range=e.ds.r6.b1,dx=eir6b1->dx,dpx=eir6b1->dpx;
constraint,sequence=lhcb1,range=e.ds.r6.b1,   mux=muxip6b1+eir6b1->mux;
constraint,sequence=lhcb1,range=e.ds.r6.b1,   muy=muyip6b1+eir6b1->muy;
if (is_thin==1){
constraint,sequence=lhcb1,range=IP6,mux=muxIP6b1_l,muy=muyIP6b1_l;
};
if(match_on_aperture==1){
constraint,range=mqml.10l6.b1,bety< 170.+7.2-6+18+epsap;
constraint,range=MQY.5L6.B1,betx<592+epsap;
constraint,range=MQY.4L6.B1,bety<610+6+epsap;
constraint,range=MQY.4R6.B1,betx<552+epsap;
constraint,range=MQY.5R6.B1,bety<600+0+epsap;
constraint,range=mqm.9r6.b1,bety< 180+epsap;
constraint,range=mqml.10r6.b1,betx< 180.+7.3+epsap;
constraint,range=mq.11r6.b1,bety< 178.+2.0+0.3+1.0+epsap;
};
vary, name=kqt13.l6b1,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
vary, name=kqt12.l6b1,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
vary, name=kqtl11.l6b1, step=1.0E-6, lower=-qtlimit4*350./550., upper=qtlimit4*350./550.;
vary, name=kq10.l6b1,   step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
vary, name=kq9.l6b1,    step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
vary, name=kq8.l6b1,    step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin*0.8;
vary, name=kq5.l6b1,    step=1.0E-6, lower= qtlimit2*scmin, upper= qtlimit2;
if (is_thin==1){
 vary,name=kq4.l6b1,   step=1.0E-6, lower=-qtlimit2, upper=-qtlimit2*scmin;
};
vary, name=kq4.r6b1,    step=1.0E-6, lower= qtlimit2*scmin, upper= qtlimit2;
vary, name=kq5.r6b1,    step=1.0E-6, lower=-qtlimit2, upper=-qtlimit2*scmin;
vary, name=kq8.r6b1,    step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
vary, name=kq9.r6b1,    step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
vary, name=kq10.r6b1,   step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
vary, name=kqtl11.r6b1, step=1.0E-6, lower=-qtlimit4*300./550., upper=qtlimit4*300./550.;
vary, name=kqt12.r6b1,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
vary, name=kqt13.r6b1,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
!lmdif,calls=200,tolerance=1.e-21;
jacobian,calls=jac_calls, tolerance=jac_tol, bisec=jac_bisec;
!simplex,  calls=15, tolerance=jac_tol;
endmatch;

exec,mk_irtwiss(6,b1);
value,kq4.l6b1+0.004833837730000;
dmuxkickb1=table(twiss,TCSG.4R6.B1,mux)- table(twiss,MKD.H5L6.B1,mux);
value,dmuxkickb1;
al_dump=761;
bxdumpb1=refbetxip6b1-2*al_dump*refalfxip6b1+
                        al_dump^2*(1+refalfxip6b1^2)/refbetxip6b1;
bydumpb1=refbetyip6b1-2*al_dump*refalfyip6b1+
                        al_dump^2*(1+refalfyip6b1^2)/refbetyip6b1;
value,bxdumpb1,bydumpb1;




if(on_aperture==1){
exec,plot_ap(6,b1,$nrj,$full);
};


tarir6b1=tar;
value,tarir6b1;
value,kqt13.l6b1,kqt12.l6b1,kqtl11.l6b1,kq10.l6b1,kq9.l6b1,kq8.l6b1,kq5.l6b1,kq4.l6b1,
kq4.r6b1,kq5.r6b1,kq8.r6b1,kq9.r6b1,kq10.r6b1,kqtl11.r6b1,kqt12.r6b1,kqt13.r6b1;


!muxip6b1 = 2.0000 ;betxip6b1= 187.297499 ;alfxip6b1 = -0.541994 ;
!muyip6b1 = 2.0300 ;betyip6b1= 202.523627 ;alfyip6b1 =  0.536786 ;
!dxip6b1 =  0.100621 ;dpxip6b1 =   3.5364e-05 ;

!strategy for 11->22 : use all  (kq4.l6b1 fixed),dx,dpx and alfas as injection, betas not constrained
!strategy for 22->28 : use all (kq4.l6b1 fixed), dx,dpx,alfx, as injection, alfy increased by 60%, betx/y not constrained (around 190 m)
!strategy for 22->82 : use all (kq4.l6b1 fixed), dx,dpx,alfx,alfay as injection, betx/y not constrained (around 280/190 m)


