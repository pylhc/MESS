! Call 2018 LHC sequences
call,file="lhc_as-built.seq"; 
seqedit,sequence=lhcb1;flatten;cycle,start=IP3;endedit;

! call 2018 collision optics
call, file="optics.str"; 

! set energy and beam parameter
NRJ=6500.0;
beam, sequence=lhcb1, bv= 1, energy=NRJ, particle=proton, npart=1.0e10, kbunch=1, ex=7.29767146889e-09,ey=7.29767146889e-09;
   		  
Use, sequence=lhcb1;
select,flag=twiss,clear;
 
TWISS;

! set up modulation steps and increment
Steps = 100;
Stepsize = 3E-8;

! save quadrupole str and set to min strength
KTQX1.R1_old = KTQX1.R1;
KTQX1.R1    = KTQX1.R1 - Steps/2. * Stepsize ;

! create table where K and tunes are saved
CREATE, TABLE=MQXA.1R1, COLUMN = K, TUNEX, ERRTUNEX, TUNEY, ERRTUNEY;

! go through modulation
n=0;
WHILE(n<=Steps){
VALUE, n;
select,flag=twiss,clear;
TWISS, table=nominal;

K=abs(TABLE(nominal, MQXA.1R1, k1l))/6.37;
TUNEX=TABLE(summ, q1);
ERRTUNEX=0;
TUNEY=TABLE(summ, q2);
ERRTUNEY=0;


FILL, TABLE=MQXA.1R1, ROW=0;

n = n+1;
KTQX1.R1    = KTQX1.R1 + Stepsize ;
};

! save table in file and reset quadrupole strength
WRITE, TABLE = MQXA.1R1, FILE="MQXA.1R1.B1.tfs";
KTQX1.R1 = KTQX1.R1_old;

