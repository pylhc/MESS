!#############################################################################
!################################ Options ####################################
!#############################################################################

NRJ = 6500;
brho := NRJ/0.3;
thisemit := 3.75e-6 / (NRJ / 0.938);

value, NRJ;
value, thisemit;

DeltaQx = -0.01;
DeltaQy = 0.012;


!#############################################################################
!############################# Setting Up Lattice ############################
!#############################################################################

call, file="lhc_as-built.seq";
slicefactor = 4;
beam;

call, file="myslice.madx";
beam;

use, sequence=lhcb1;
makethin, sequence=lhcb1, style=teapot, makedipedge=true;


!#############################################################################
!############################ Install the AC Dipole ##########################
!#############################################################################

MKACH.6L4.B1: hacdipole, l=0, freq:=Qxd, lag=0, volt:=voltx, ramp1=0000, ramp2=2000, ramp3=8600, ramp4=9000;
MKACV.6L4.B1: vacdipole, l=0, freq:=Qyd, lag=0, volt:=volty, ramp1=0000, ramp2=2000, ramp3=8600, ramp4=9000;

  seqedit, sequence=lhcb1; flatten;
  install, element=MKACH.6L4.B1, at=0.0, from=MKQA.6L4.B1;
  install, element=MKACV.6L4.B1, at=0.0, from=MKQA.6L4.B1;
  endedit;

! Cycling w.r.t. to IP3 (mandatory to find closed orbit in collision in the presence of errors)
seqedit, sequence=lhcb1; flatten; cycle, start=IP3; endedit;


!#############################################################################
!######################## Call Injection Optics File #########################
!#############################################################################

call, file="opticsfile.1";
beam, sequence=lhcb1, bv= 1, energy=NRJ, particle=proton, npart=1.0e10, kbunch=1, ex=7.29767146889e-09, ey=7.29767146889e-09;
USE, SEQUENCE=lhcb1;


!#############################################################################
!################### Set Up AC Dipole Tunes and Strengths ####################
!#############################################################################

twiss;
Qx0 = table(summ, Q1);
Qy0 = table(summ, Q2);

!AC Dipole tune
Qxd = Qx0 + DeltaQx;
Qyd = Qy0 + DeltaQy;

USE, sequence=lhcb1;
twiss;
betxac = table(twiss, MKACH.6L4.B1, betx);
betyac = table(twiss, MKACV.6L4.B1, bety);

kick_1sigmax = SQRT(thisemit) * brho * ABS(DeltaQx) * 4 * PI / SQRT(betxac);
kick_1sigmay = SQRT(thisemit) * brho * ABS(DeltaQy) * 4 * PI / SQRT(betyac);

voltx = 1 * kick_1sigmax;
volty = 1 * kick_1sigmay;

VALUE, brho;
VALUE, voltx;
VALUE, volty;
VALUE, betxac;
VALUE, betyac;
VALUE, Qxd;
VALUE, Qyd;


!#############################################################################
!############################# Perform Tracking ##############################
!#############################################################################

USE, sequence=lhcb1;
TRACK, RECLOSS, ONEPASS=True, DUMP=True;
START, X=0.0, Y=0.0;
OBSERVE, place=BPM.21R8.B1;
RUN, TURNS=9000;
ENDTRACK;